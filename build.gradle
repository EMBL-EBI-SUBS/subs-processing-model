buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'uk.ac.ebi.subs'
version '2.14.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    api("uk.ac.ebi.subs:subs-data-model:2.14.1-SNAPSHOT")

    compileOnly("org.projectlombok:lombok:1.18.10")
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    testImplementation("org.springframework.boot:spring-boot-starter-test:2.2.1.RELEASE")
}

task jarWithAddedManifest(type: Jar) {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Source-Compatibility': project.sourceCompatibility)
    }
    with jar
}

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'subs-processing-model'

            pom {
                name = 'subs-processing-model'
                description = 'Data Submission Portal - common library for processing related data model'
                url = 'https://github.com/EMBL-EBI-SUBS/subs-processing-model'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'subs-internal'
                        name = 'EMBL-EBI-SUBS'
                        email = 'subs-internal@ebi.ac.uk'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/EMBL-EBI-SUBS/subs-processing-model.git'
                    developerConnection = 'scm:git:ssh://github.com/EMBL-EBI-SUBS/subs-processing-model.git'
                    url = 'https://github.com/EMBL-EBI-SUBS/subs-processing-model'

                }
            }

            from components.java
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

